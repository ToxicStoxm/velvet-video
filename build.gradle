import com.vanniktech.maven.publish.SonatypeHost

plugins {
    id 'java-library'
    id "com.vanniktech.maven.publish" version "0.30.0"
}

group = 'com.toxicstoxm.velvet-video-remastered'
version = '0.7.1'

ext {
    descr = 'Java video encoding/decoding/muxing/demuxing library'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.toxicstoxm.YAJL:YAJL:1.4.3'

    implementation 'com.github.jnr:jnr-ffi:2.2.17'
    implementation 'commons-io:commons-io:2.18.0'

    compileOnly 'org.projectlombok:lombok:1.18.36'
    compileOnly 'org.jetbrains:annotations:24.0.0'
    annotationProcessor 'org.projectlombok:lombok:1.18.36'
    annotationProcessor 'org.jetbrains:annotations:24.0.0'

    testImplementation 'com.github.fracpete:musicg:1.4.2.2'
}

tasks.jar {
    dependsOn('fatJar')
}

tasks.register('fatJar', Jar) {
    archiveBaseName = "${rootProject.name}-fat"
    duplicatesStrategy = DuplicatesStrategy.INCLUDE

    from { configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) } }

    with jar
}


mavenPublishing {
    publishToMavenCentral(SonatypeHost.CENTRAL_PORTAL)
    signAllPublications()

    coordinates("com.toxicstoxm.velvet-video-remastered", "velvet-video-remastered", version)

    pom {
        name = "velvet-video-remastered"
        description = "Java video encoding/decoding/muxing/demuxing library"
        inceptionYear = "2024"
        url = "https://github.com/ToxicStoxm/velvet-video-remastered/"

        licenses {
            license {
                name = "The GNU General Public License, Version 3.0"
                url = "https://www.gnu.org/licenses/gpl-3.0.html"
                distribution = "https://www.gnu.org/licenses/gpl-3.0.html"
            }
        }

        developers {
            developer {
                id = "toxicstoxm"
                name = "ToxicStoxm"
                url = "https://github.com/ToxicStoxm"
            }
            developer {
                id = "zakgof"
                name = "Oleksandr Zakusylo"
                url = "https://github.com/zakgof"
                email = "zakgof@gmail.com"
            }
        }

        scm {
            url = "https://github.com/ToxicStoxm/velvet-video-remastered.git"
            connection = "scm:git:git://github.com/ToxicStoxm/velvet-video-remastered.git"
            developerConnection = "scm:git:ssh://git@github.com/ToxicStoxm/velvet-video-remastered.git"
        }
    }
}